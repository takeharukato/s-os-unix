NAME(0x24AC)

ファイル名を変更する

破壊レジスタ:
保存レジスタ:

引数:
	DE 変更するファイル名

返り値:
	C=1
	            Aレジスタにエラーコードが設定される
		    - 3(Bad file descriptor) ドライブレターがディスクでない
		    - 11(Reserved feature)   ドライブレターが標準ディスクでない

NAME:
	LD	A,(#DSK)	; ドライブレターを取得
	CALL	ALCHK		; ドライブレターが標準ディスクであることを確認する
	RET	C		; テープや標準ディスクでない場合はエラー復帰
	;
	PUSH	DE		; DEを保存
	CALL	FCBSCH		; DEが示すファイルのファイルのディレクトリエントリを取得
	LD	(DEBUF),DE	; ディレクトリエントリのレコード番号(DE)を保存
	LD	(HLBUF),HL	; 変更前のディレクトリエントリ格納先アドレス
	POP	DE		; DEを復元
	RET	C		; FCBSCHがエラー(Disk Error)を返している場合はエラー復帰
	LD	A,8		; File not foundを返す
	SCF			; キャリーをセット
	RET	NZ		; 一致するファイルが見つからなかった場合はエラー復帰

	LD	A,(HL)		; ディレクトリエントリの先頭(ファイル属性)をロード
	CALL	WPCHK		; 書込禁止になっていないことを確認
	RET	C		; 書込禁止の場合は, エラー復帰
	;
	LD	A,(#DSK)	; ドライブレターを取得
	PUSH	AF		; AFを保存
	CALL	#FILE		; 変更後のファイル名をセット
	POP	AF		; AFを復元
	LD	(#DSK),A	; ドライブレターをセット
				; (#FILEでセットしたドライブレターを上書き)
	CALL	FCBSCH		; DEが示すファイルのファイルのディレクトリエントリを取得
	RET	C		; FCBSCHがエラー(Disk Error)を返している場合はエラー復帰
	LD	A,10		; File Already Existsをセット
	SCF			; キャリーをセット
	RET	Z		; 変更後のファイル名と同ファイル名のファイルが存在する
	LD	DE,(DEBUF) 	; ディレクトリエントリのレコード番号(DE)を取得
	LD	HL,(#DTBUF)	; ディレクトリエントリ格納先アドレスをHLにロード
	LD	A,1		; 1レコード読み込む
	CALL	DSKRED		; レコードの内容を#DTBUFの参照先にロード
	RET	C		; Disk errorの場合はエラー復帰
	LD	HL,(#IBFAD)	; 変更後のディレクトリエントリのアドレスをHLにロード
	INC	HL		; ファイル名以降のアドレスを指す
	LD	DE,(HLBUF)	; 変更前ディレクトリエントリのアドレスをDEにロード
	INC	DE		; ファイル名以降の内容を指す
	LD	BC, 17		; 変更前ディレクトリエントリ中のファイル名を更新
	LDIR	    		;
	LD	DE,(DEFBUF)	; ディレクトリエントリのレコード番号(DE)を取得
	LD	HL,(#DTBUF)	; ディレクトリエントリ格納先アドレスをHLにロード
	LD	A,1		; 1レコード書き込む
	CALL	DSKWRT		; レコード(ディレクトリエントリ)を更新
	RET


WPCHK(0x257c)

ライトプロテクトチェックを行う
ファイルの属性値の書込禁止ビット(0x40)が立っていた場合,
キャリーをセットして, Aレジスタに4をセットして復帰する。

破壊レジスタ: AF
保存レジスタ:

引数:
	A           ファイルの属性値
返り値:
	C=1
	            Aレジスタにエラーコードが設定される
		    - 4(Write protected)     書込禁止

WPCHK:
	OR	A	; キャリーをクリアする
	BIT	6,A	; 6ビット目が立っているか確認する
	RET	Z	; ビットが立っていなければ呼び出し元に復帰
	LD	A,4	; Write protected
	SCF		; キャリーをセット
	RET		; 呼び出し元に復帰
