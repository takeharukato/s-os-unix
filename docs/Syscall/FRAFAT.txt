ERAFAT(0x274E)

ファイルに割り当てられたFATエントリを解放する

引数:
	A	ファイルの最初のクラスタのクラスタ番号

破壊レジスタ:AF,
保存レジスタ:DE, BC, HL

返り値:
* Aレジスタ=0, フラグ: C=0 正常終了
* Aレジスタ=7, フラグ: C=1 不正なFATを検出した

ERAFAT:
	PUSH	DE
	PUSH	HL
	LD	DE, (#FATBF)	; FATBFのアドレスを取得

ERAFA1:
	LD	L, A		; 削除対象クラスタのクラスタ番号(FATのインデクス)を
	LD	H, 0		; HLにロード
	ADD	HL, DE		; 削除対象クラスタに対応するFATのアドレスを算出
	LD	A, (HL)		; 次のクラスタのクラスタ番号(FATのインデクス)
	LD	(HL), 0		; クラスタを未使用に設定
	CP	$80   		; 最終クラスタに到達したか(0x80より小さいか)?
	JR	C, ERAFA1	; 最終クラスタに到達していなければ次のクラスタの処理へ
	POP	HL
	POP	DE
	CP	$90		; 最終クラスタのクラスタ値が0x90以上なら
	JR	NC, ERAFA2	; 最終クラスタ内使用レコード数不正により復帰
	XOR	A   		; 正常終了時は0を返して復帰する
	RET

ERAFA2:
	LD	A, 7		; Bad allocation tableをセット
	SCF	   		; キャリーをセットする
	RET
